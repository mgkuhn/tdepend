#!/usr/bin/perl
#List *.tex and *.m files on the command line to get Makefile dependencies

sub scan_texfile {
    my ($fn, $src) = @_;
    my $f;
    my @dep = ();
    open($f, "<$fn") or die("tdepend: '$fn' listed in $src: $!\n");
    while (<$f>) {
	if (/\\(includegraphics)\*?(?:\[\S+\]){0,2}\{([\w\.\/-]+)\}/ ||
	    /\\(input)\*?(?:\[\S+\]){0,2}\{([\w\.\/-]+)\}/) {
	    my $command = $1;
	    my $path = $2;
	    if ($command eq 'input') {
		$path .= '.tex' unless $path =~ /\.\w{1,8}$/i;
	    }
	    push @dep, $path;
	    # recurse
	    push @dep, scan_texfile($path, "$fn (line $.)")
		if $path =~ /.tex$/i;
	}
    }
    close($f);
    return @dep;
}

foreach $fn (@ARGV) {
    if ($fn =~ /\.tex$/i) {
	my @dep = scan_texfile($fn, 'command line');
	$fn =~ s/\.tex$/\.dvi/i;
	print "$fn: " . join(' ', @dep) . "\n\n" if @dep;
    } elsif ($fn =~ /\.m$/) {
	my $cmd = $`;
	my $prod;
	my $dep = $fn;
	open(F, "<$fn");
	while (<F>) {
	    # handle continuation lines
	    while (s/\.\.\.\s*$//) {
		$_ .= <F>;
	    }
	    if (/^\s*(imwrite|saveas)\(.*?'([\w\.%-]+)\'.*?(,\s*\'\w+\'\s*)?\);?\s*$/) {
		$prod .= ' ' if $prod;
		$prod .= $2;
	    }
            if (/^\s*load\s+([\w\.%-\@]+)\s*(;.*)?\s*$/) {
                $dep .= " $1"
            }
	}
        close(F);
	if ($prod && !$matlab) {
	    print "MATLAB=matlab -nosplash\n\n";
	    $matlab = 1;
	}
	print "$prod: $dep\n\techo $cmd | \$(MATLAB)\n\n" if $prod;
    } else {
	die("Unknown file type of '$fn'");
    }
}
