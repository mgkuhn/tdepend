#!/usr/bin/perl
#List *.tex and *.m files on the command line to get Makefile dependencies

use strict;

unless (@ARGV) {
    print STDERR <<'EOT';
Scans the LaTeX (*.tex) and MATLAB (*.m) files provided as command-line
arguments for dependencies (files that they include), and writes the
result out in form rules suitable for inclusion into a Makefile.

Usage: tdepend [options] file file ...

Options:

  -p           Assume that pdflatex is used, i.e. that file *.tex produces
               a target *.pdf (the default assumes a target *.dvi) and
	       that \includegraphics will add a default suffix of .pdf.

  -s<suffix>   Add additional suffixes to the default LaTeX target list.

               Example: if you use your exam.tex source file to produce
               both exam.pdf and exam-solutions.pdf files, use
               "tdepend -p -s-solutions.pdf exam.tex >Makefile.include".

  -k           Also try kpsewhich to find dependencies.

EOT
    exit 1;
}

my @tex_target_suffix = ('.dvi');
my $includegraphics_suffix = 'eps';
my $use_kpsewhich;

# remove duplicates from array
sub uniq {
    my %seen;
    grep !$seen{$_}++, @_;
}

sub scan_texfile {
    my ($fn, $src) = @_;
    my $f;
    my @dep = ();
    my $path = $fn;
    if ($use_kpsewhich && ! -e $fn) {
	die if $fn =~ /'/;
	$path = `kpsewhich '$fn'` // $fn;
	chomp $path;
    }
    open($f, '<', $path) or die("tdepend: '$path' listed in $src: $!\n");
    while (<$f>) {
	s/(?<!\\)%.*$//;  # remove comments
	last if /^\s*\\end\{document\}/;
	if (/^(.*?)\\begin\{verbatim\}(.*)$/) {
	    # skip verbatim blocks
	    my $prelude = $1;
	    $_ = $2;
	    if (!s/^.*\\end\{verbatim\}//) {
		while (defined($_ = <$f>) && !s/^.*\\end\{verbatim\}//) {}
	    }
	    $_ = $prelude . $_;
	}
	while (s/\\includegraphics\*?(?:\[[^\]]*\]){0,2}\{([\w\.\/-]+)\}//) {
	    my $path = $1;
	    $path .= $includegraphics_suffix
		unless $path =~ /\.[a-zA-Z_]+$/;
	    push @dep, $path;
	}
	while (s/\\includepdf(?:\[[^\]]*\]){0,2}\{([\w\.\/-]+)\}//) {
	    my $path = $1;
	    $path .= '.pdf'
		unless $path =~ /\.[a-zA-Z_]+$/;
	    push @dep, $path;
	}
	while (s/\\epsfig\{figure=([\w\.\/-]+)(?:,[^,\}]*)*\}//) {
	    my $path = $1;
	    push @dep, $path;
	}
	while(s/\\input\*?(?:\[\S+\]){0,2}\{([\w\.\/-]+)\}//) {
	    my $path = $1;
	    $path .= '.tex' unless $path =~ /\.\w{1,8}$/i;
	    push @dep, $path;
	    # recurse
	    push @dep, scan_texfile($path, "$fn (line $.)")
		if $path =~ /\.tex$/i;
	}
	while(s/\\bibliography\{(.*?)\}//) {
	    my @bibs = split(/,\s*/, $1);
	    my $bbl = $fn;
	    $bbl =~ s/\.tex$/.bbl/;
	    print "$bbl: ", join(' ', map { "$_.bib" } @bibs), "\n\n";
	    push @dep, $bbl;
	}
    }
    close($f);
    return uniq(@dep);
}

my $matlab;
foreach my $fn (@ARGV) {
    if ($fn eq '-p') {
	# assume use of pdftex/pdflatex: set both the target suffix
	# and the \includegraphics default suffix to .pdf
	@tex_target_suffix = ('.pdf');
	$includegraphics_suffix = 'pdf';
    } elsif ($fn eq '-k') {
	$use_kpsewhich = 1;
    } elsif ($fn =~ /^-s(.*)$/i) {
	# add additional target suffixes
	push @tex_target_suffix, $1;
    } elsif ($fn =~ /\.tex$/i) {
	my @dep = scan_texfile($fn, 'command line');
	$fn =~ s/\.tex$//i;
	print join(' ', map({ $fn . $_ } @tex_target_suffix)) .
	    ": " . join(' ', @dep) . "\n\n" if @dep;
    } elsif ($fn =~ /\.m$/) {
	my $cmd = $`;
	my $prod;
	my $dep = $fn;
	my $f;
	open($f, "<$fn");
	while (<$f>) {
	    # handle continuation lines
	    while (s/\.\.\.\s*$//) {
		$_ .= <$f>;
	    }
	    if (/^\s*(imwrite|saveas)\(.*?'([\w\.%-]+)\'.*?(,\s*\'\w+\'\s*)?\);?\s*$/) {
		$prod .= ' ' if $prod;
		$prod .= $2;
	    }
            if (/^\s*load\s+([\w\.%-\@]+)\s*(;.*)?\s*$/) {
                $dep .= " $1"
            }
	}
        close($f);
	if ($prod && !$matlab) {
	    print "MATLAB=matlab -nosplash\n\n";
	    $matlab = 1;
	}
	print "$prod: $dep\n\techo $cmd | \$(MATLAB)\n\n" if $prod;
    } else {
	die("Unknown file type of '$fn'");
    }
}
